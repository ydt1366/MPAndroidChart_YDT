apply plugin: 'com.android.library'

group='com.github.philjay'

android {
    namespace="com.github.mikephil.charting"
    compileSdkVersion 36
    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 36
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    testOptions {
        unitTests.returnDefaultValues = true // this prevents "not mocked" error
    }
}

dependencies {
    implementation 'androidx.annotation:annotation:1.9.1'
    testImplementation 'junit:junit:4.13.2'
}

tasks.register('sourcesJar', Jar) {
    from android.sourceSets.main.java.srcDirs
    archiveClassifier.set('sources')
}

tasks.register('javadoc', Javadoc) {
    options.charSet = 'UTF-8'
    failOnError false
    source = android.sourceSets.main.java.sourceFiles
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

tasks.register('javadocJar', Jar) {
    dependsOn javadoc
    archiveClassifier.set('javadoc')
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}
java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}
